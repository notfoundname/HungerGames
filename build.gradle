import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "${SHADOW_VERSION}"
}

defaultTasks "clean", "build", "shadowJar"

group = "${HUNGERGAMES_GROUP}.hg"
version = "${HUNGERGAMES_VERSION}"

repositories {
    mavenCentral()
    maven { url "https://jitpack.io/" }
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url "https://papermc.io/repo/repository/maven-public/" }
    maven { url "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
    maven { url "https://repo.codemc.org/repository/maven-public/" }
    maven { url "https://nexus.hc.to/content/repositories/pub_releases/" }
    maven { url "https://jitpack.io/" }
    maven { url "https://simonsator.de/repo/" }
    maven { url "https://repo.alessiodp.com/releases/" }
    maven { url "https://mvn.lumine.io/repository/maven-public/" }
    maven { url "https://maven.maxhenkel.de/repository/public" }
}

dependencies {
    implementation "io.papermc:paperlib:${PAPERLIB_VERSION}"
    implementation "org.bstats:bstats-bukkit:${BSTATS_BUKKIT_VERSION}"
    implementation "org.apache.commons:commons-lang3:${COMMONS_LANG3_VERSION}"

    compileOnly "org.spigotmc:spigot-api:${SPIGOT_API_VERSION}"
    compileOnly "me.clip:placeholderapi:${PLACEHOLDERAPI_VERSION}"
    compileOnly "de.simonsator:Party-and-Friends-MySQL-Edition-Spigot-API:${PARTY_AND_FRIENDS_VERSION}"
    // TODO CVE-2022-45690
    compileOnly "de.simonsator:Spigot-Party-API-For-RedisBungee:${SPIGOT_PARTY_API_VERSION}"
    compileOnly "de.simonsator:DevelopmentPAFSpigot:${DEVELOPMENTPAFSPIGOT_VERSION}"
    compileOnly "com.alessiodp.parties:parties-api:${PARTIES_API_VERSION}"
    compileOnly "io.lumine:Mythic-Dist:${MYTHIC_DIST_VERSION}"
    compileOnly ("net.milkbowl.vault:VaultAPI:${VAULTAPI_VERSION}") {
        exclude group: "org.bukkit", module: "bukkit"
    }
    compileOnly "de.tr7zw:item-nbt-api-plugin:${ITEM_NBT_API_VERSION}"
    compileOnly ("com.gmail.nossr50.mcMMO:mcMMO:${MCMMO_VERSION}") {
        exclude group: "com.sk89q.worldguard", module: "worldguard-core"
        exclude group: "com.sk89q.worldguard", module: "worldguard-legacy"
        exclude group: "org.apache.maven.scm", module: "maven-scm-provider-gitexe"
    }
    compileOnly "de.maxhenkel.voicechat:voicechat-api:${VOICECHAT_API_VERSION}"
}

processResources {
    filter ReplaceTokens, tokens: [ "version" : HUNGERGAMES_VERSION ]
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
compileJava.options.encoding = "UTF-8"

shadowJar {
	relocate "org.bstats", "${HUNGERGAMES_GROUP}"
    relocate "io.papermc.lib", "${HUNGERGAMES_GROUP}.hg.api.paperlib"
}